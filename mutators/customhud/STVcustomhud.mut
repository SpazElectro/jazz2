#pragma require "STVutil.asc"
#include "STVutil.asc"
#include "../../scripts/STVutil.asc"

bool toggled_on = true;

bool locatingItem = false;
string itemBeingLocated = "";

string settingsVersion = "0.1A";

array<int> upgradedIds;
array<int> unupgradedIds;

int gridSize = 10;

AnimatedSprite@ gun = AnimatedSprite(ANIM::AMMO, 0, 0, 0, 0.25, false);

int snap(int spot) {
    return (spot + gridSize / 2) / gridSize * gridSize;
}

bool upgraded(jjPLAYER @player, int id)
{
    return player.powerup[id];
}

int idToSprite(jjPLAYER@ player, int id)
{
    if(id == 1) {
        if(gun.id == ANIM::PLUS_AMMO && player.charCurr != CHAR::LORI) gun.setId(ANIM::AMMO);

        if(player.charCurr == CHAR::JAZZ) {
            return 18; // jazz blaster
        } else if(player.charCurr == CHAR::SPAZ) {
            return 19; // spaz blaster
        } else if(player.charCurr == CHAR::LORI) {
            gun.setId(ANIM::PLUS_AMMO);
            
            return 5;
        }
    }

    if (upgraded(player, id))
    {
        return upgradedIds[id - 2];
    }
    else
    {
        jjConsole("" + id + " - " + (id - 2));
        return unupgradedIds[id - 2];
    }
}

jjSTREAM saveSettings() {
    jjSTREAM settingsStream = jjSTREAM();
    settingsStream.push(settingsVersion);
    settingsStream.push(toggled_on);
    
    // the magic starts here
    settingsStream.push(formatInt(ammoX));
    settingsStream.push(formatInt(ammoY));
    settingsStream.push(formatSize(ammoSize));
    
    settingsStream.push(formatInt(healthX));
    settingsStream.push(formatInt(healthY));
    settingsStream.push(formatInt(healthSize));
    settingsStream.push(formatInt(healthPadding));
    
    settingsStream.push(formatInt(flagX));
    settingsStream.push(formatInt(flagY));
    settingsStream.push(formatInt(flagSize));
    // the magic ends here

    settingsStream.save("STVcustomhud-" + p.nameUnformatted + ".asdat");
    
    return settingsStream;
}

void loadSettings(jjSTREAM settingsStream) {
    string version;
    if (!settingsStream.pop(version)) {
        jjConsole("[CH] Version not found!");
        return;
    }

    if (version != settingsVersion) {
        jjConsole("[CH] Version mismatch found!");
        return;
    }

    if (!settingsStream.pop(toggled_on)) {
        jjConsole("[CH] Settings not found!");
        return;
    }

    string ammoXStr, ammoYStr, ammoSizeStr;
    settingsStream.pop(ammoXStr);
    settingsStream.pop(ammoYStr);
    settingsStream.pop(ammoSizeStr);

    ammoX = parseInt(ammoXStr);
    ammoY = parseInt(ammoYStr);
    ammoSize = parseSize(ammoSizeStr);

    string healthXStr, healthYStr, healthSizeStr, healthPaddingStr;
    
    settingsStream.pop(healthXStr);
    settingsStream.pop(healthYStr);
    settingsStream.pop(healthSizeStr);
    settingsStream.pop(healthPaddingStr);
    
    healthX = parseInt(healthXStr);
    healthY = parseInt(healthYStr);
    healthSize = parseInt(healthSizeStr);
    healthPadding = parseInt(healthPaddingStr);

    string flagXStr, flagYStr, flagSizeStr;
    
    settingsStream.pop(flagXStr);
    settingsStream.pop(flagYStr);
    settingsStream.pop(flagSizeStr);

    flagX = parseInt(flagXStr);
    flagY = parseInt(flagYStr);
    flagSize = parseInt(flagSizeStr);
}

void onLevelBegin() {
    jjConsole("[CH] Started!");
    
    jjSTREAM settingsStream("STVcustomhud-" + p.nameUnformatted + ".asdat");
    
    jjConsole("" + settingsStream.isEmpty());
    if(settingsStream.isEmpty()) settingsStream = saveSettings();
    loadSettings(settingsStream);

    jjConsole("[CH] Loaded settings!");

    unupgradedIds.insertLast(25); // bouncer
    unupgradedIds.insertLast(29); // ice
    unupgradedIds.insertLast(34); // seeker
    unupgradedIds.insertLast(49); // rf
    unupgradedIds.insertLast(57); // toaster
    unupgradedIds.insertLast(59); // tnt
    unupgradedIds.insertLast(62); // fireball
    unupgradedIds.insertLast(68); // electroblaster

    upgradedIds.insertLast(24); // bouncer
    upgradedIds.insertLast(28); // ice
    upgradedIds.insertLast(33); // seeker
    upgradedIds.insertLast(48); // rf
    upgradedIds.insertLast(56); // toaster
    upgradedIds.insertLast(59); // tnt
    upgradedIds.insertLast(61); // fireball
    upgradedIds.insertLast(67); // electroblaster

    jjConsole("[CH] Loaded ids!");
}

void onPlayer(jjPLAYER@ player) {
    if(locatingItem) {
        int x = snap(jjMouseX);
        int y = snap(jjMouseY);
        
        if(itemBeingLocated == "ammo") {
            ammoX = x;
            ammoY = y;
        }
        
        if(itemBeingLocated == "health") {
            healthX = x;
            healthY = y;
        }

        if(itemBeingLocated == "flag") {
            flagX = x;
            flagY = y;
        }

        if(isKeyDown(getKeyById("LeftMouse"))) {
            locatingItem = false;
        }
    }

    gun.setId(idToSprite(player, player.currWeapon));
}

void onMain() {
    gun.update();
}

bool onLocalChat(string &in stringReceived, CHAT::Type chatType) {
    if(stringReceived == "!locateammo") {
        locatingItem = true;
        itemBeingLocated = "ammo";
        
        return true;
    }

    if(stringReceived == "!locatehealth") {
        locatingItem = true;
        itemBeingLocated = "health";
        
        return true;
    }

    if(stringReceived == "!locateflag") {
        locatingItem = true;
        itemBeingLocated = "flag";
        
        return true;
    }

    return false;
}

// the magic starts here

int ammoX = 0;
int ammoY = 0;
STRING::Size ammoSize = STRING::MEDIUM;

int healthX       = 0;
int healthY       = 0;
int healthSize    = 1;
int healthPadding = 15;

int flagX      = 0;
int flagY      = 0;
int flagSize   = 1;

// the magic ends here

bool onDrawAmmo(jjPLAYER@ player, jjCANVAS@ canvas) {
    // utils
    if(locatingItem) {
        int x = ammoX;
        int y = ammoY;

        if(itemBeingLocated == "health") { x = healthX; y = healthY; }
        if(itemBeingLocated == "flag") { x = flagX; y = flagY; }

        x += 10;

        canvas.drawString(x, y - 10, "" + snap(jjMouseX) + " - " + snap(jjMouseY), STRING::SMALL);
        canvas.drawString(x, y - 20, "Ratio: " + (jjResolutionWidth - snap(jjMouseX)) + "x" + (jjResolutionHeight - snap(jjMouseY)), STRING::SMALL);
        canvas.drawString(x, y - 30, "Center ratio: " + ((jjResolutionWidth / 2) - snap(jjMouseX)) + "x" + ((jjResolutionHeight / 2) - snap(jjMouseY)), STRING::SMALL);
        canvas.drawString(x, y - 40, "Bottom center ratio: " + ((jjResolutionWidth / 2) - snap(jjMouseX)) + "x" + (jjResolutionHeight - snap(jjMouseY)), STRING::SMALL);
    }

    if(toggled_on) {
        string ammo = "" + player.ammo[player.currWeapon];
        if(player.currWeapon == 1) ammo = "^";
        canvas.drawString(ammoX, ammoY, "x" + ammo, ammoSize);
        
        gun.x = ammoX - 10;
        gun.y = ammoY;
        gun.draw(canvas);
        return true;
    } else { return false; }
}

bool onDrawHealth(jjPLAYER@ player, jjCANVAS@ canvas) {
    if(toggled_on) {
        for(int i = 0; i < player.health; i++) {
            canvas.drawResizedSprite(healthX + (i * (healthPadding * healthSize)), healthY, ANIM::PICKUPS, 41, 0, healthSize, healthSize);
        }

        // flag

        if(jjGameMode == GAME::CTF) {
            //                                                                                                  5 is green or red - it's temporary
            canvas.drawResizedSprite(flagX, flagY, ANIM::FLAG, player.team == TEAM::RED ? 7 : (player.team == TEAM::BLUE ? 3 : 5), 0, flagSize, flagSize);
        }

        return true;
    } else { return false; }
}

// bool onDrawLives(jjPLAYER@ player, jjCANVAS@ canvas) {return true;}
// bool onDrawPlayerTimer(jjPLAYER@ player, jjCANVAS@ canvas) {return true;}
// bool onDrawScore(jjPLAYER@ player, jjCANVAS@ canvas) {return true;}
// bool onDrawGameModeHUD(jjPLAYER@ player, jjCANVAS@ canvas) {return true;}
