#pragma name "cartoonfade"

bool isFading = false;
float delta = 0;
int lastTime = jjGameTicks;

const float DEFAULT_SCALE = 0.0;
const float SCALE_STEP = 0.05;

float scale = DEFAULT_SCALE;

void initFade() {
	jjAnimSets[45].load(
		jjPIXELMAP(
			#asset "fade.png"
		),
		frameWidth: 500,
		frameHeight: 500,
		frameSpacingX: 0,
		frameSpacingY: 0,
		startY: 0,
		firstAnimToOverwrite: jjAnimSets[45]
	);
}

void updateFade() {
	// calculate delta time
	delta = (jjGameTicks - lastTime);
	lastTime = jjGameTicks;

	if (isFading) {
		scale += SCALE_STEP * delta;

		if (scale >= 4) {
			scale = DEFAULT_SCALE;
			isFading = false;
			jjNxt(false, true);
		}
	}
}

void drawFade(jjPLAYER@ player, jjCANVAS@ canvas) {
	canvas.drawResizedSprite(
		int(player.xPos), int(player.yPos),
		45, 0, 0,
		scale, scale
	);
}

void playerDrawFade(jjPLAYERDRAW& pd) {
	pd.name = !isFading;
}

void onLevelLoad() {
	initFade();
}

void onMain() {
	updateFade();
}

void onDrawLayer3(jjPLAYER@ player, jjCANVAS@ canvas) {
	drawFade(player, canvas);
}

bool onLocalChat(string &in stringReceived, CHAT::Type chatType) {
	if (stringReceived == "fade") {
		isFading = true;
		return true;
	}

	return false;
}

void onPlayerDraw(jjPLAYERDRAW& pd) {
	playerDrawFade(pd);
}